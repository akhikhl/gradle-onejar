// This script is applied to every subproject.
// It should not contain any project-specific information.

apply plugin: 'maven'

repositories {
  mavenLocal()
  mavenCentral()
}

afterEvaluate {

  group = (rootProject.ext.has('group') ? rootProject.ext.group : group)
  version = (version == 'unspecified' ? (rootProject.ext.has('version') ? rootProject.ext.version : '0.0.1') : version)

  def groovy_version = rootProject.ext.has('groovy_version') ? rootProject.ext.groovy_version : '2.1.9'
  def junit_version = rootProject.ext.has('junit_version') ? rootProject.ext.junit_version : '4.11'
  def spock_version = rootProject.ext.has('spock_version') ? rootProject.ext.spock_version : '0.7-groovy-2.0'

  if(plugins.findPlugin('java')) {
    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'
    [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
  }

  if(plugins.findPlugin('groovy')) {
    dependencies {
      compile "org.codehaus.groovy:groovy-all:$groovy_version"
    }
  }

  if(plugins.findPlugin('java') || plugins.findPlugin('groovy')) {

    if(tasks.findByName('test')) {
      dependencies {
        testCompile "junit:junit:$junit_version"
        if(!plugins.findPlugin('groovy'))
          testCompile "org.codehaus.groovy:groovy-all:$groovy_version"
        testCompile "org.spockframework:spock-core:$spock_version"
      }
    }

    if(project.parent.name == 'libs') {
      // lib projects should be always installed into "$HOME/.m2"
      project.tasks.build.finalizedBy project.tasks.install
    }

    if(project.parent.name == 'apps' || project.parent.name == 'examples') {
      // apps and examples should not be uploaded to maven repository
      project.tasks.uploadArchives.enabled = false
    }

    task('createFolders', description: 'Creates the source folders if they do not exist.') << {
      sourceSets*.allSource*.srcDirs*.each { File srcDir ->
        if (!srcDir.isDirectory()) {
          println "Creating source folder: ${srcDir}"
          srcDir.mkdirs()
        }
      }
    }

    if(generateSources && tasks.findByName('classes')) {
      task sourcesJar(type: Jar, dependsOn: classes, description: 'Creates sources jar') {
        classifier = 'sources'
        from sourceSets.main.allSource
      }
      artifacts {
        archives sourcesJar
      }
    }

    if(generateJavadoc && tasks.findByName('javadoc')) {
      task javadocJar(type: Jar, description: 'Creates javadoc jar') {
        dependsOn javadoc
        classifier = 'javadoc'
        from javadoc.destinationDir
        if(tasks.findByName('groovydoc')) {
          dependsOn groovydoc
          from groovydoc.destinationDir
        }
      }
      artifacts {
        archives javadocJar
      }
    }
  }
}
